/*
 * @Description: 安装窗口修改自网上分享的 Rush hour Package.mzp, 安装窗版权归原作者
 * @Author: Bullet.S
 * @Date: 2019-08-18 22:17:43
 * @LastEditors: Bullet.S
 * @LastEditTime: 2019-11-09 23:39:00
 * @Email: animator.bullet@foxmail.com
 */

try(destroyDialog mzpDialog)catch()

Global arrLog = #()
Global mzpDialog
Global numVer = 6
Global posMouMoved = [0,0]
Global fnMouseState = false
Global fnAddUpdateLog
-----------------------------------------------------------------------------------------------------------------INSTALL FILES
--these are the locations the mzp package extracted the files to
Global file1 = "BulletTools.ms"
Global theMacroFile = "_[BulletTools]-BulletTools.mcr"
--the array of files which have to be moved, deleted or installed
Global myScriptFileArray = #(file1) --the scriptfiles which have to be copied
-- Global myTotalFileArray = #(file1,theMacroFile) --the scriptfiles including the macroscript
-----------------------------------------------------------------------------------------------------------------INSTALL FILES

fn fnAddUpdateLog arrLog =
(
-------------------------------------------------------↓ 添加更新记录描述 -----------------------------------------------------------------
	for i = numVer to 1 by -1 do
	(
		local str = "rollout rolLogV" + i as string + " \" v" + (i/10) as string + "." + ((mod i 10)as integer) as string + " \"\r\n(\r\n	label labelV" + i as string + " \"\" pos:[10,10] width:270 height:205 style_sunkenedge:true \r\n)"
		strRol = "rolLogV" + i as string
		strAdd = "append arrLog rolLogV" + i as string
		logRolTemp = (execute strRol) 
		logRolTemp = execute str
		execute strAdd
	)
	fn fnAddLogCap numLog caption =
	(
		local str = "rolLogV" + numLog as string + ".labelV" + numLog as string + ".caption = \"" + caption + "\""
		execute str
	)
	addSubRollout mzpDialog.srolUpdateLog  arrLog[1]  rolledUp:false
	for i = 2 to arrLog.count do
	(
		addSubRollout mzpDialog.srolUpdateLog arrLog[i] rolledUp:true
	)
	fnAddLogCap 6 "2019.10.04  v0.6\r\n\r\n1. 修复浮动窗位置保存有误的bug\r\n\r\n2. 添加改变帧率和播放速度的快捷按钮\r\n\r\n3. GitHub开源脚本并添加许可证版权说明等,\r\n\r\n大佬们可随意优化,\r\n\r\n但请勿商用!\r\n\r\n"
	fnAddLogCap 5 "2019.10.03  v0.5\r\n\r\n1. 优化工具宽度(强迫症),按钮位置极致对称(开玩笑的\r\n\r\n2. 修改快捷操作中文字颜色,使其在深色主题中也能看清\r\n\r\n3. 修复只有一个脚本类别文件夹时高度显示(脚本类别被遮住)问题~\r\n\r\n推荐更新!"
	fnAddLogCap 4 "2019.10.01  v0.4\r\n\r\n1. 优化很多插件体验比如根据脚本数量调整UI高度\r\n\r\n2. 预备新增快捷操作按钮和实用功能,只显示图标,右键功能提示\r\n\r\n3. 修复很多报错bug,具体哪些我也忘了...推荐更新!\r\n\r\n4. 有其他建议可以私我~ 欢迎交流讨论"
	fnAddLogCap 3 "2019.08.19  v0.3\r\n\r\n1. 修复新暴力粘贴插件打不开的问题,\r\n\r\n2. 添加更新帖的链接,右键设置里面~"
	fnAddLogCap 2 "2019.08.xx  v0.2\r\n\r\n太远古忘记更新啥了..."
	fnAddLogCap 1 "2019.08.xx  v0.1\r\n\r\n太远古忘记更新啥了..."
)
----------------------------------------------------------------------------------------------------------------------------------------

rollout mzpDialog " BulletTools 安装工具 " width:500 height:385
(
	local textUpdateLink = (" BulletTools 安装 --- [ 当前版本 v"+ (numVer/10) as string + "." + ((mod numVer 10)as integer) as string + " ]")

	dotNetControl tbxResult "textBox" readonly:true Multiline:true height:100 width:195 pos:[10,25]

	button btnRemove "解除该脚本自启" pos:[22,160] width:170 enabled:false tooltip:"解除工具随MAX启动,设置里也有"
	button btnInstall "安装更新并自启该脚本" pos:[22,185] width:170 enabled:true tooltip:"安装工具并随MAX启动"
	button btnOpenFolder "打开自定脚本目录" pos:[22,210] width:170 enabled:true tooltip:"打开自定义脚本目录自定工具按钮"
	-- button btnUpdateLog "版本更新记录" pos:[22,235] width:170 enabled:true tooltip:"版本更新记录清单"
	button btnUpdateLink "查看更新和下载" pos:[22,235] width:170 enabled:true tooltip:"查看更新下载"
	HyperLink lnkLink "| 2019.9  miHoYo_Bullet.S |"
	color:(color 255 20 100) hovercolor:(color 255 0 255) visitedcolor:(color 255 20 100) \
	pos:[40,260] address:"https://space.bilibili.com/2031113"
	label labmiHoYo "--- TECH OTAKUS SAVE THE WORLD --" pos:[15,280]
	GroupBox grbTips "" pos:[5,5] width:205 height:130
	GroupBox grbInstall " 安装 " pos:[5,140] width:205 height:160
	GroupBox grbUpdateLog " 更新记录 ------------中键此处关闭安装窗口" pos:[220,5] width:275 height:295
	subrollout srolUpdateLog "" pos:[225,23] width:265 height:270
	bitmap the_bmp pos:[5,303] width:490 height:80
	
	--check if there are scriptfiles present in the userscripts-folder with the same names as the files we want to install
	fn fn_checkScriptFilePresence =
	(
		local dotNetStringArray = #()
		--test if the files are already present
		for fileString in myScriptFileArray do
		(
			if doesFileExist ((getDir #startupScripts)+"\\"+fileString) do
			(
				append dotNetStringArray (dotNetObject "system.string" fileString)
			)
		)
		dotNetStringArray
	)
	
	fn fn_updateInterface  logBox logMessage oldFileArray button1=
	(
		if oldFileArray.count > 0 do button1.enabled = true --if there are old files present, enable the button to remove them
		--populate the listboxes
		--display a message
		logBox.text = logMessage
	)
	
	on mzpDialog open do
	(
		local theArray = fn_checkScriptFilePresence()
		local logTips
		
		mzpDialog.grbTips.caption = textUpdateLink as string
		logTips = "专为动画师定制的脚本整合工具,\r\n自定义脚本文件生成按钮和分类!\r\n活到老, 学到老, K到老! 保持激情~\r\n-... ..- .-.. .-.. . - .-.-.- ...\r\n\r\n作者: Bullet.S     插件诞生: 2019.08\r\n@Email: animator.bullet@foxmail.com"
		fn_updateInterface tbxResult logTips theArray btnRemove
		arrLog = #()
		-- if rolUpdateLog != undefined then closeRolloutFloater rolUpdateLog
		fnAddUpdateLog arrLog
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)
	
	on mzpDialog close do
	(
		--delete the files from a temp location, clean up
		for fileString in myScriptFileArray do
		(
			deleteFile ((getDir #temp)+"\\"+fileString) --delete any file with the same name on this location
		)
		deleteFile ((getDir #temp)+"\\"+theMacroFile) --delete the macro
	)
	
		-----------------------------------------------------------------------------------------
	on mzpDialog mbuttondown pos do 
	(
		try (destroydialog mzpDialog) catch ()
	)
	
	on mzpDialog lbuttondown posMou do
	(
		posMouMoved = posMou
		fnMouseState = on
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)
	
	on mzpDialog lbuttonup posMou do
	(
		fnMouseState = off
	)
	
	on mzpDialog mouseMove pos do
	(
		if fnMouseState == on then
		(
			SetDialogPos mzpDialog (mouse.screenpos - posMouMoved)			
		)
	)
	---------------------上面设置拖动脚本窗口,去掉标题栏后默认无法拖动---------------------
	
	on btnInstall pressed do
	(
		-- local stream
		--process the scriptfiles
		copyFile ((getDir #temp)+"\\_Blzt.dll") ("c:\\Blzt")
		copyFile ((getDir #temp)+"\\02.mse") ("c:\\Blzt")
		for fileString in myScriptFileArray do
		(
			if doesFileExist ((getDir #startupScripts)+"\\"+fileString + "e") then 
			(
				setFileAttribute ((getDir #startupScripts)+"\\"+fileString + "e") #readOnly false
				deleteFile ((getDir #startupScripts)+"\\"+fileString + "e")
			)
			if doesFileExist ((getDir #startupScripts)+"\\"+fileString) then 
			(
				setFileAttribute ((getDir #startupScripts)+"\\"+fileString) #readOnly false
				deleteFile ((getDir #startupScripts)+"\\"+fileString) --delete any file with the same name on this location
			)
			copyFile ((getDir #temp)+"\\"+fileString) ((getDir #startupScripts)+"\\"+fileString) --move the file to the userscripts
			deleteFile ((getDir #Scripts)+"\\"+fileString) --delete any file with the same name on this location
			deleteFile ((getDir #Scripts)+"\\"+fileString + "e") 
			copyFile ((getDir #temp)+"\\"+fileString) ((getDir #Scripts)+"\\"+fileString) --move the file to the userscripts
		)
		--process the macro
		if doesFileExist ((getDir #userMacros)+"\\"+"BulletTools.mcr") then 
		(
			setFileAttribute ((getDir #userMacros)+"\\"+"BulletTools.mcr") #readOnly false
			deleteFile ((getDir #userMacros)+"\\"+"BulletTools.mcr")
		)
		deleteFile ((getDir #userMacros)+"\\"+theMacroFile) --delete the macro
		copyFile ((getDir #temp)+"\\"+theMacroFile) ((getDir #userMacros)+"\\"+theMacroFile) --move the macro to the usermacros
		--execute the macro. this puts the script in a button
		fileIn ((getDir #userMacros)+"\\"+theMacroFile)
		--update the interface
		local theArray = fn_checkScriptFilePresence()
		local myMessage = "工具安装成功. 你可以在\r\n \"Customize\" -- \"Customize user interface...\" -- \"Toolbars\" -- \"_[BulletTools]\" Category (最上) \r\n中找到,并拖到工具栏~"
		fn_updateInterface tbxResult myMessage theArray btnRemove
		FileIn ((getDir #startupScripts) + "\\" + "BulletTools.ms")
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)

	on btnRemove pressed do
	(
		for fileString in myScriptFileArray do
		(
			if doesFileExist ((getDir #startupScripts)+"\\"+fileString) then 
			(
				setFileAttribute ((getDir #startupScripts)+"\\"+fileString) #readOnly false
				deleteFile ((getDir #startupScripts)+"\\"+fileString) --delete any file with the same name on this location
			) --delete any file with the same name on this location
		)

		local theArray = fn_checkScriptFilePresence()
		local myMessage = "已解除该脚本自启\r\n( 并不是卸载~ )\r\n( 设置中可开启自启 )"
		fn_updateInterface tbxResult myMessage theArray btnRemove
		btnRemove.enabled = false
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)
	
	on btnOpenFolder pressed do
	(
		makeDir ((getDir #scripts)+"\\BulletS")
		ShellLaunch "explorer" ((getDir #scripts)+"\\BulletS")
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)

	on btnUpdateLink pressed do  --左键点击跳转链接
	(
		shellLaunch "https://www.cgjoy.com/forum.php?mod=viewthread&tid=224775" ""
		the_bmp.fileName = (getdir #scripts) + "\\BulletScripts\\Icons\\Background" + (random 1 2) as string + ".png"
	)
)
-- try(destroyDialog mzpInstall)catch()
createDialog mzpDialog fgcolor:(color 255 20 100) style:#()